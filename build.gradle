buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springbootVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'club.tempvs'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter:${springbootVersion}"
    compile "org.springframework.boot:spring-boot-starter-web:${springbootVersion}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springbootVersion}"
    compile "org.postgresql:postgresql:${postgresVersion}"
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    //testing
    integrationTestCompile "org.springframework.boot:spring-boot-starter-test:${springbootVersion}"
    integrationTestRuntime 'com.h2database:h2:1.4.194'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.18.3'
}

bootJar {
    baseName = 'tempvs-message'
    version =  project.version
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task stage() {
    dependsOn build
}
