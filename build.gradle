buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springbootVersion}"
    }
}

plugins {
    id 'org.liquibase.gradle' version '2.0.1'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'club.tempvs'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter:${springbootVersion}"
    compile "org.springframework.boot:spring-boot-starter-web:${springbootVersion}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springbootVersion}"
    compile "org.postgresql:postgresql:${postgresVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    compile "org.liquibase:liquibase-core:${liquibaseVersion}"
    //testing
    integrationTestCompile "org.springframework.boot:spring-boot-starter-test:${springbootVersion}"
    integrationTestRuntime 'com.h2database:h2:1.4.194'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.18.3'
    //liquibase
    liquibaseRuntime "org.liquibase:liquibase-core:${liquibaseVersion}"
    liquibaseRuntime "org.postgresql:postgresql:${postgresVersion}"
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
    //amqp
    compile 'com.rabbitmq:amqp-client:3.3.4'
}

bootJar {
    baseName = 'tempvs-message'
    version =  project.version
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task stage() {
    dependsOn build
}

task liquibaseDiffChangelog(type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibaseRuntime
    main = "liquibase.integration.commandline.Main"

    args "--changeLogFile=" + 'src/main/resources/db/changelog/changes/' + System.getenv('LIQUIBASE_DIFF_FILENAME')
    args "--referenceUrl=" + 'hibernate:spring:club.tempvs.message.domain?dialect=org.hibernate.dialect.PostgreSQL95Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
    args "--username=" + System.getenv('JDBC_DATABASE_USERNAME')
    args "--password=" + System.getenv('JDBC_DATABASE_PASSWORD')
    args "--url=" + System.getenv('JDBC_DATABASE_URL')
    args "--driver=org.postgresql.Driver"
    args "diffChangeLog"
}

task liquibaseGenerateChangeLog(type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibaseRuntime
    main = "liquibase.integration.commandline.Main"

    args "--changeLogFile=" + 'src/main/resources/db/changelog/db.changelog-master.yaml'
    args "--referenceUrl=" + 'hibernate:spring:club.tempvs.message.domain?dialect=org.hibernate.dialect.PostgreSQL95Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
    args "--username=" + System.getenv('JDBC_DATABASE_USERNAME')
    args "--password=" + System.getenv('JDBC_DATABASE_PASSWORD')
    args "--url=" + System.getenv('JDBC_DATABASE_URL')
    args "--driver=org.postgresql.Driver"
    args "generateChangeLog"
}
