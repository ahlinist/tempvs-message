buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.7.RELEASE"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
    }
}

plugins {
    id 'net.ltgt.apt' version '0.10'
    id 'org.liquibase.gradle' version '2.0.1'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog/changes/' + System.getenv('LIQUIBASE_DIFF_FILENAME')
            url System.getenv('JDBC_DATABASE_URL')
            referenceUrl 'hibernate:spring:club.tempvs.message.domain?dialect=org.hibernate.dialect.PostgreSQL95Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
            username System.getenv('JDBC_DATABASE_USERNAME')
            password System.getenv('JDBC_DATABASE_PASSWORD')
        }
    }
}

group 'club.tempvs'
version '1.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR2'
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix'
    compile 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    //database
    compile "org.postgresql:postgresql"
    //lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    //testing
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testRuntime 'com.h2database:h2:1.4.194'
    //liquibase
    implementation "org.liquibase:liquibase-core" //to fire liquibase on start
    liquibaseRuntime "org.liquibase:liquibase-core"
    liquibaseRuntime "org.postgresql:postgresql"
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
    liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
    liquibaseRuntime sourceSets.main.output
    //amqp
    compile 'com.rabbitmq:amqp-client:3.3.4'
}

bootJar {
    archiveBaseName = 'tempvs-message'
    archiveVersion = ''
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }

        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}
